#!/usr/bin/env coffee
noflo_root = "#{__dirname}/.."
noflo = require "#{noflo_root}/lib/NoFlo"

process.on "uncaughtException", (e) ->
    app.styles.red "#{e.stack}\n"
    e.stopPropagation()
    app.prompt()

shell = require "shell"

app = new shell.Shell
    project_dir: noflo_root

app.setPrompt = (prompt) ->
    @settings.prompt = "#{prompt}>> "

app.configure ->
    app.use shell.history
        shell: app
    app.use shell.router
        shell: app
    app.use shell.help
        shell: app
        introduction: true

app.setPrompt "NoFlo"
app.network = null
app.filename = null

app.cmd "new *", "Create a new NoFlo graph", (req, res, next) ->
    graph = noflo.graph.createGraph req.params[0]
    app.setPrompt req.params[0]
    app.network = noflo.createNetwork graph
    delete app.filename
    res.prompt()

app.cmd "load *", "Load a NoFlo graph", (req, res, next) ->
    app.filename = req.params[0]
    noflo.loadFile app.filename, (network) ->
        app.network = network
        app.setPrompt app.network.graph.name
        res.prompt()

app.cmd "save *", "Save a NoFlo graph", (req, res, next) ->
    app.filename = req.params[0]
    unless app.network
        app.styles.red "No graph is loaded"
        return res.prompt()
    noflo.saveFile app.network.graph, app.filename, ->
        app.styles.green "Saved to #{app.filename}"
        res.prompt()

app.cmd "save", "Save a NoFlo graph", (req, res, next) ->
    unless app.network
        app.styles.red "No graph is loaded"
        return res.prompt()
    unless app.filename
        app.styles.red "No filename known, use 'save <somefile>'"
        return res.prompt()
    noflo.saveFile app.filename, app.network.graph, ->
        app.styles.green "Saved to #{app.filename}"
        res.prompt()

app.cmd "close", "Close a NoFlo graph", (req, res, next) ->
    unless app.network
        app.styles.red "No graph is loaded"
        return res.prompt()
    delete app.network
    delete app.filename
    app.setPrompt "NoFlo"
    res.prompt()

app.cmd "add :id :component", "Add a node to the graph", (req, res, next) ->
    unless app.network
        app.styles.red "No graph is loaded"
        return res.prompt()
    app.network.graph.addNode req.params.id, req.params.component
    res.prompt()

app.cmd "remove :id", "Remove a node from the graph", (req, res, next) ->
    unless app.network
        app.styles.red "No graph is loaded"
        return res.prompt()
    app.network.graph.removeNode req.params.id
    res.prompt()

app.cmd "connect :fromid :fromport :toid :toport", "Connect nodes in graph", (req, res, next) ->
    unless app.network
        app.styles.red "No graph is loaded"
        return res.prompt()
    app.network.graph.addEdge req.params.fromid, req.params.fromport, req.params.toid, req.params.toport
    res.prompt()

app.cmd "disconnect :id :port", "Disconnect a node in graph", (req, res, next) ->
    unless app.network
        app.styles.red "No graph is loaded"
        return res.prompt()
    app.network.graph.removeEdge req.params.id, req.params.port
    res.prompt()

app.cmd "send :id :port :data", "Send data packet to a node", (req, res, next) ->
    unless app.network
        app.styles.red "No graph is loaded"
        return res.prompt()
    app.network.graph.addInitial req.params.data, req.params.id, req.params.port
    res.prompt()

app.cmd "dot", "Get a DOT visualization of a graph", (req, res, next) ->
    unless app.network
        app.styles.red "No graph is loaded"
        return res.prompt()
    app.styles.blue app.network.graph.toDOT()
    res.prompt()
